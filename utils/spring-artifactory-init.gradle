initscript {
	ext {
		artifactoryPluginVersion = '4.33.12'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:$artifactoryPluginVersion"
	}
}

def ossrhUrl = System.getenv('OSSRH_URL')
def gpgPassphrase = System.getenv('GPG_PASSPHRASE')
def gpgPrivateKey = System.getenv('GPG_PRIVATE_KEY')

allprojects {

	apply plugin: 'signing'

	tasks.register('artifactoryPublish', org.jfrog.gradle.plugin.artifactory.task.ArtifactoryTask) {
		setCiServerBuild()
	}

	apply plugin: org.jfrog.gradle.plugin.artifactory.ArtifactoryPlugin

	artifactory {
		publish {
			contextUrl = System.getenv('ARTIFACTORY_URL')
			repository {
				repoKey = System.getenv('ARTIFACTORY_REPOSITORY')
				username = System.getenv('ARTIFACTORY_USERNAME')
				password = System.getenv('ARTIFACTORY_PASSWORD')
			}
			defaults {
				publications 'mavenJava'
				def zipArtifactProps =
						['zip.name'       : project.name,
						 'zip.displayname': project.description,
						 'zip.deployed'   : 'false']
				properties {
					mavenJava zipArtifactProps, '*:*:*:*@zip'
					mavenJava 'zip.type': 'docs', '*:*:*:docs@zip'
					mavenJava 'zip.type': 'dist', '*:*:*:dist@zip'
				}
			}
			publishForkCount = 10
		}

		buildInfo {
			setProject(System.getenv('ARTIFACTORY_BUILD_PROJECT'))
			setBuildName(System.getenv('ARTIFACTORY_BUILD_NAME'))
			setBuildNumber(System.getenv('ARTIFACTORY_BUILD_NUMBER'))
			setBuildUrl(System.getenv('ARTIFACTORY_BUILD_URL'))
			setAgentName(System.getenv('ARTIFACTORY_USER_AGENT_NAME'))
			setAgentVersion(System.getenv('ARTIFACTORY_USER_AGENT_VERSION'))
			setVcsRevision(System.getenv('ARTIFACTORY_VCS_REVISION'))
			setVcsUrl(System.getenv('ARTIFACTORY_VCS_URL'))
			setArtifactoryPluginVersion(ext.artifactoryPluginVersion)
		}

		clientConfig.connectionRetries = 4
		clientConfig.insecureTls = false
	}

	if (!ossrhUrl && gpgPassphrase && gpgPrivateKey) {
		afterEvaluate {
			pluginManager.withPlugin('maven-publish') {
				signing {
					useInMemoryPgpKeys(gpgPrivateKey, gpgPassphrase)
					sign publishing.publications.mavenJava
				}
			}
		}
	}

}
